-- PARAMETRIZACIÓN DE LOS TRAMOS DE MONTO
-- Definición de los rangos de monto para aplicar puntos adicionales.
VAR b_tramo_a_min NUMBER;
EXEC :b_tramo_a_min := 500000;
VAR b_tramo_a_max NUMBER;
EXEC :b_tramo_a_max := 700000;

VAR b_tramo_b_min NUMBER;
EXEC :b_tramo_b_min := 700001;
VAR b_tramo_b_max NUMBER;
EXEC :b_tramo_b_max := 900000;

--CASO_1
DECLARE
    -- VARIABLES DE CONTROL
    -- Obtiene el año anterior a la fecha actual para procesar las transacciones del año pasado.
    V_ANIO_ANTERIOR NUMBER := EXTRACT(YEAR FROM SYSDATE) - 1;
    
    -- DEFINICIÓN DEL VARRAY PARA PUNTOS
    -- Se define un arreglo con los valores de puntos base y adicionales según los tramos especificados.
    TYPE PUNTOS_ARRAY IS VARRAY(4) OF NUMBER;
    V_PUNTOS PUNTOS_ARRAY := PUNTOS_ARRAY(250, 300, 550, 700);
    
    -- Declaración de VARRAY clientes con puntos extra
    TYPE VARRAY_CLIENTES_PTOS_EXTRA IS VARRAY(2) OF NUMBER;
    clientes_ptos_extra VARRAY_CLIENTES_PTOS_EXTRA := VARRAY_CLIENTES_PTOS_EXTRA(30, 40);
    
    V_CLIENTE_TIPO NUMBER(3,0);
    V_PUNTOS_EXTRAS NUMBER := 0;


    -- CURSOR PARA OBTENER LAS FECHAS DE TRANSACCIONES
    -- Recupera las fechas únicas de transacciones del año anterior.
    CURSOR C_RESUMEN_TRANSACCION IS 
        SELECT DISTINCT FECHA_TRANSACCION
        FROM TRANSACCION_TARJETA_CLIENTE
        WHERE EXTRACT(YEAR FROM FECHA_TRANSACCION) = V_ANIO_ANTERIOR
        ORDER BY FECHA_TRANSACCION;

    -- CURSOR EXPLÍCITO CON PARÁMETRO PARA TRANSACCIONES DETALLADAS
    -- Obtiene los detalles de las transacciones de una fecha específica.
    CURSOR C_DETALLE_TRANSACCION(P_FECHA_TRANSACCION DATE) IS
        SELECT C.NUMRUN, C.DVRUN, T.NRO_TARJETA, T.NRO_TRANSACCION, T.FECHA_TRANSACCION, 
            TT.NOMBRE_TPTRAN_TARJETA AS TIPO_TRANSACCION, T.MONTO_TRANSACCION, C.COD_TIPO_CLIENTE
        FROM TRANSACCION_TARJETA_CLIENTE T
        JOIN TARJETA_CLIENTE TC ON T.NRO_TARJETA = TC.NRO_TARJETA
        JOIN CLIENTE C ON TC.NUMRUN = C.NUMRUN
        JOIN TIPO_TRANSACCION_TARJETA TT ON T.COD_TPTRAN_TARJETA = TT.COD_TPTRAN_TARJETA
        WHERE T.FECHA_TRANSACCION = P_FECHA_TRANSACCION
        ORDER BY C.NUMRUN, T.NRO_TRANSACCION;
        

    -- VARIABLE PARA ALMACENAR DATOS DE LA TRANSACCIÓN
    R_TRANSACCION DETALLE_PUNTOS_TARJETA_CATB%ROWTYPE;
    V_COUNT NUMBER;

BEGIN
    -- TRUNCAR TABLAS ANTES DE INICIAR
    EXECUTE IMMEDIATE 'TRUNCATE TABLE DETALLE_PUNTOS_TARJETA_CATB';
    EXECUTE IMMEDIATE 'TRUNCATE TABLE RESUMEN_PUNTOS_TARJETA_CATB';

    -- RECORRER CADA FECHA DE TRANSACCIÓN
    FOR R_RESUMEN_TRANSACCION IN C_RESUMEN_TRANSACCION LOOP
        DBMS_OUTPUT.PUT_LINE('PROCESANDO FECHA: ' || TO_CHAR(R_RESUMEN_TRANSACCION.FECHA_TRANSACCION, 'DD/MM/YY'));
        FOR R_DETALLE_TRANSACCION IN C_DETALLE_TRANSACCION(R_RESUMEN_TRANSACCION.FECHA_TRANSACCION) LOOP
            -- Verificación de duplicados antes de insertar
            SELECT COUNT(*) INTO V_COUNT
            FROM DETALLE_PUNTOS_TARJETA_CATB
            WHERE NUMRUN = R_DETALLE_TRANSACCION.NUMRUN 
              AND NRO_TARJETA = R_DETALLE_TRANSACCION.NRO_TARJETA 
              AND NRO_TRANSACCION = R_DETALLE_TRANSACCION.NRO_TRANSACCION;

            IF V_COUNT = 0 THEN
                -- Asignación de valores a la variable de transacción
                R_TRANSACCION.NUMRUN := R_DETALLE_TRANSACCION.NUMRUN;
                R_TRANSACCION.DVRUN := R_DETALLE_TRANSACCION.DVRUN;
                R_TRANSACCION.NRO_TARJETA := R_DETALLE_TRANSACCION.NRO_TARJETA;
                R_TRANSACCION.NRO_TRANSACCION := R_DETALLE_TRANSACCION.NRO_TRANSACCION;
                R_TRANSACCION.FECHA_TRANSACCION := R_RESUMEN_TRANSACCION.FECHA_TRANSACCION;
                R_TRANSACCION.TIPO_TRANSACCION := R_DETALLE_TRANSACCION.TIPO_TRANSACCION;
                R_TRANSACCION.MONTO_TRANSACCION := R_DETALLE_TRANSACCION.MONTO_TRANSACCION;

                -- Cálculo de puntos
                V_CLIENTE_TIPO := R_DETALLE_TRANSACCION.COD_TIPO_CLIENTE;
        
                IF V_CLIENTE_TIPO IN ( clientes_ptos_extra(1), clientes_ptos_extra(2) ) THEN 
                    CASE 
                        WHEN R_TRANSACCION.MONTO_TRANSACCION BETWEEN :b_tramo_a_min AND :b_tramo_a_max THEN 
                            v_puntos_extras := TRUNC(R_TRANSACCION.MONTO_TRANSACCION / 100000 ) * V_PUNTOS(2) + TRUNC(R_TRANSACCION.MONTO_TRANSACCION / 100000 ) * V_PUNTOS(1);
                        WHEN R_TRANSACCION.MONTO_TRANSACCION BETWEEN :b_tramo_b_min AND :b_tramo_b_max THEN 
                            v_puntos_extras := TRUNC(R_TRANSACCION.MONTO_TRANSACCION / 100000 ) * V_PUNTOS(3) + TRUNC(R_TRANSACCION.MONTO_TRANSACCION / 100000 ) * V_PUNTOS(1);
                        WHEN R_TRANSACCION.MONTO_TRANSACCION > :b_tramo_b_max THEN 
                            v_puntos_extras := TRUNC(R_TRANSACCION.MONTO_TRANSACCION / 100000 ) * V_PUNTOS(4) + TRUNC(R_TRANSACCION.MONTO_TRANSACCION / 100000 ) * V_PUNTOS(1);
                        ELSE
                            v_puntos_extras := TRUNC(R_TRANSACCION.MONTO_TRANSACCION / 100000 ) * V_PUNTOS(1) + TRUNC(R_TRANSACCION.MONTO_TRANSACCION / 100000 ) * V_PUNTOS(1);
                    END CASE;
                    
                ELSE
                    v_puntos_extras := TRUNC(R_TRANSACCION.MONTO_TRANSACCION / 100000 ) * V_PUNTOS(1);
                END IF;
        
                    R_TRANSACCION.PUNTOS_ALLTHEBEST := v_puntos_extras;


                -- Insertar la transacción en la tabla de detalle
                INSERT INTO DETALLE_PUNTOS_TARJETA_CATB 
                    VALUES R_TRANSACCION;
                    
                
            END IF;
        END LOOP;
    END LOOP;

    -- LLENADO DE LA TABLA RESUMEN
    INSERT INTO RESUMEN_PUNTOS_TARJETA_CATB (MES_ANNO, MONTO_TOTAL_COMPRAS, TOTAL_PUNTOS_COMPRAS, MONTO_TOTAL_AVANCES, TOTAL_PUNTOS_AVANCES, MONTO_TOTAL_SAVANCES, TOTAL_PUNTOS_SAVANCES)
    SELECT TO_CHAR(FECHA_TRANSACCION, 'YYYYMM'),
           SUM(CASE WHEN TIPO_TRANSACCION LIKE 'Compra%' THEN MONTO_TRANSACCION ELSE 0 END),
           SUM(CASE WHEN TIPO_TRANSACCION LIKE 'Compra%' THEN PUNTOS_ALLTHEBEST ELSE 0 END),
           SUM(CASE WHEN TIPO_TRANSACCION LIKE 'Avance%' THEN MONTO_TRANSACCION ELSE 0 END),
           SUM(CASE WHEN TIPO_TRANSACCION LIKE 'Avance%' THEN PUNTOS_ALLTHEBEST ELSE 0 END),
           SUM(CASE WHEN TIPO_TRANSACCION LIKE 'Súper Avance%' THEN MONTO_TRANSACCION ELSE 0 END),
           SUM(CASE WHEN TIPO_TRANSACCION LIKE 'Súper Avance%' THEN PUNTOS_ALLTHEBEST ELSE 0 END)
    FROM DETALLE_PUNTOS_TARJETA_CATB
    GROUP BY TO_CHAR(FECHA_TRANSACCION, 'YYYYMM')
    ORDER BY TO_CHAR(FECHA_TRANSACCION, 'YYYYMM');

    COMMIT;
    DBMS_OUTPUT.PUT_LINE('PROCESO COMPLETADO EXITOSAMENTE PARA EL AÑO ' || V_ANIO_ANTERIOR);
EXCEPTION
    WHEN OTHERS THEN
        ROLLBACK;
        DBMS_OUTPUT.PUT_LINE('ERROR: ' || SQLERRM);
END;
/

SELECT * FROM DETALLE_PUNTOS_TARJETA_CATB;
SELECT * FROM RESUMEN_PUNTOS_TARJETA_CATB;

--CASO_2
DECLARE
    -- VARIABLES DE CONTROL
    -- Obtiene el año actual para procesar las transacciones del año en curso.
    V_ANIO_ACTUAL NUMBER := EXTRACT(YEAR FROM SYSDATE);
    V_APORTE NUMBER;
    V_MONTO_TOTAL NUMBER;

    -- CURSOR PARA OBTENER LAS FECHAS DE TRANSACCIONES
    -- Recupera las fechas únicas de transacciones del año actual.
    CURSOR C_FECHAS_TRANSACCION IS 
        SELECT DISTINCT FECHA_TRANSACCION
        FROM TRANSACCION_TARJETA_CLIENTE
        WHERE EXTRACT(YEAR FROM FECHA_TRANSACCION) = V_ANIO_ACTUAL
        ORDER BY 
        FECHA_TRANSACCION;
        
      

    -- CURSOR EXPLÍCITO CON PARÁMETRO PARA TRANSACCIONES DETALLADAS
    -- Obtiene los detalles de las transacciones de una fecha específica.
    CURSOR C_TRANSACCIONES(P_FECHA DATE) IS
        SELECT C.NUMRUN, C.DVRUN, T.NRO_TARJETA, T.NRO_TRANSACCION, T.FECHA_TRANSACCION, 
               TT.NOMBRE_TPTRAN_TARJETA AS TIPO_TRANSACCION, T.MONTO_TOTAL_TRANSACCION AS MONTO_TOTAL
        FROM TRANSACCION_TARJETA_CLIENTE T
        JOIN TARJETA_CLIENTE TC ON T.NRO_TARJETA = TC.NRO_TARJETA
        JOIN CLIENTE C ON TC.NUMRUN = C.NUMRUN
        JOIN TIPO_TRANSACCION_TARJETA TT ON T.COD_TPTRAN_TARJETA = TT.COD_TPTRAN_TARJETA
        WHERE T.FECHA_TRANSACCION = P_FECHA
          AND TT.COD_TPTRAN_TARJETA IN (102, 103);

    -- VARIABLE PARA ALMACENAR DATOS DE LA TRANSACCIÓN
    R_TRANSACCION DETALLE_APORTE_SBIF%ROWTYPE;

BEGIN
    -- TRUNCAR TABLAS ANTES DE INICIAR
    EXECUTE IMMEDIATE 'TRUNCATE TABLE DETALLE_APORTE_SBIF';
    EXECUTE IMMEDIATE 'TRUNCATE TABLE RESUMEN_APORTE_SBIF';

    -- PROCESAR TRANSACCIONES
    FOR R_FECHA IN C_FECHAS_TRANSACCION LOOP
        FOR R_TRANSACCION IN C_TRANSACCIONES(R_FECHA.FECHA_TRANSACCION) LOOP
            -- Calcular el monto total de la transacción
            V_MONTO_TOTAL := R_TRANSACCION.MONTO_TOTAL;

            -- Obtener el porcentaje de aporte basado en los tramos
            SELECT PORC_APORTE_SBIF INTO V_APORTE
            FROM TRAMO_APORTE_SBIF
            WHERE V_MONTO_TOTAL BETWEEN TRAMO_INF_AV_SAV AND TRAMO_SUP_AV_SAV;

            -- Insertar registro en la tabla DETALLE_APORTE_SBIF
            INSERT INTO DETALLE_APORTE_SBIF 
            VALUES (R_TRANSACCION.NUMRUN, R_TRANSACCION.DVRUN, R_TRANSACCION.NRO_TARJETA, 
                    R_TRANSACCION.NRO_TRANSACCION, R_TRANSACCION.FECHA_TRANSACCION, 
                    R_TRANSACCION.TIPO_TRANSACCION, V_MONTO_TOTAL, V_MONTO_TOTAL * (V_APORTE / 100));
        END LOOP;
    END LOOP;

    -- INSERTAR DATOS RESUMIDOS
    INSERT INTO RESUMEN_APORTE_SBIF 
    SELECT TO_CHAR(FECHA_TRANSACCION, 'YYYYMM'), TIPO_TRANSACCION, 
           SUM(MONTO_TRANSACCION), SUM(APORTE_SBIF)
    FROM DETALLE_APORTE_SBIF
    GROUP BY TO_CHAR(FECHA_TRANSACCION, 'YYYYMM'), TIPO_TRANSACCION;

    COMMIT;
    DBMS_OUTPUT.PUT_LINE('PROCESO COMPLETADO EXITOSAMENTE');

EXCEPTION
    WHEN OTHERS THEN
        ROLLBACK;
        DBMS_OUTPUT.PUT_LINE('ERROR: ' || SQLERRM);
END;
/


SELECT * FROM DETALLE_APORTE_SBIF;
SELECT * FROM RESUMEN_APORTE_SBIF;
